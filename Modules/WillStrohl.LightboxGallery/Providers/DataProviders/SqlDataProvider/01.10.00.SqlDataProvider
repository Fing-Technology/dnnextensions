/*

INSTALL SQL PROVIDER SCRIPT

Lightbox Gallery Module for DotNetNuke
Version: 
	01.10.00
Contributors: 
	Will Strohl (http://www.WillStrohl.com), 
	Armand Datema (http://www.schwingsoft.com)
Website: 
	http://wnslightbox.codeplex.com

Copyright 2011 Will Strohl

Release Notes:
  Placeholder.

*/

/*

Adding auditing information to Lightbox

*/

IF NOT EXISTS (SELECT 1 FROM [dbo].[syscolumns] c JOIN [dbo].[sysobjects] o ON c.[id] = o.[id] WHERE c.[name] = N'LastUpdatedBy' AND o.[name] LIKE N'%{objectQualifier}wns_lightbox')
    ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox] 
    ADD [LastUpdatedBy] INT NOT NULL CONSTRAINT [DF_{objectQualifier}wns_lightbox_LastUpdatedBy] DEFAULT ((1));
GO
	
IF NOT EXISTS (SELECT * FROM [dbo].[sysobjects] WHERE [id] = OBJECT_ID(N'FK_{objectQualifier}wns_lightbox_LastUpdatedBy') AND OBJECTPROPERTY([id], N'IsConstraint') = 1)
	ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox] 
	ADD CONSTRAINT [FK_{objectQualifier}wns_lightbox_LastUpdatedBy] FOREIGN KEY ([LastUpdatedBy]) 
	REFERENCES {databaseOwner}[{objectQualifier}Users] ([UserId]);
GO

IF NOT EXISTS (SELECT 1 FROM [dbo].[syscolumns] c JOIN [dbo].[sysobjects] o ON c.[id] = o.[id] WHERE c.[name] = N'LastUpdatedDate' AND o.[name] LIKE N'%{objectQualifier}wns_lightbox')
    ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox] 
    ADD [LastUpdatedDate] DATETIME NULL;
GO

UPDATE {databaseOwner}[{objectQualifier}wns_lightbox] SET [LastUpdatedDate] = GETDATE();
GO

ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox] ALTER COLUMN [LastUpdatedDate] DATETIME NOT NULL;
GO

/*

Adding auditing information to Lightbox Images

*/

IF NOT EXISTS (SELECT 1 FROM [dbo].[syscolumns] c JOIN [dbo].[sysobjects] o ON c.[id] = o.[id] WHERE c.[name] = N'LastUpdatedBy' AND o.[name] LIKE N'%{objectQualifier}wns_lightbox_image')
    ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox_image] 
    ADD [LastUpdatedBy] INT NOT NULL CONSTRAINT [DF_{objectQualifier}wns_lightbox_image_LastUpdatedBy] DEFAULT ((1));
GO
	
IF NOT EXISTS (SELECT * FROM [dbo].[sysobjects] WHERE [id] = OBJECT_ID(N'FK_{objectQualifier}wns_lightbox_image_LastUpdatedBy') AND OBJECTPROPERTY([id], N'IsConstraint') = 1)
	ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox_image] 
	ADD CONSTRAINT [FK_{objectQualifier}wns_lightbox_image_LastUpdatedBy] FOREIGN KEY ([LastUpdatedBy]) 
	REFERENCES {databaseOwner}[{objectQualifier}Users] ([UserId]);
GO

IF NOT EXISTS (SELECT 1 FROM [dbo].[syscolumns] c JOIN [dbo].[sysobjects] o ON c.[id] = o.[id] WHERE c.[name] = N'LastUpdatedDate' AND o.[name] LIKE N'%{objectQualifier}wns_lightbox_image')
    ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox_image] 
    ADD [LastUpdatedDate] DATETIME NULL;
GO

UPDATE {databaseOwner}[{objectQualifier}wns_lightbox_image] SET [LastUpdatedDate] = GETDATE();
GO

ALTER TABLE {databaseOwner}[{objectQualifier}wns_lightbox_image] ALTER COLUMN [LastUpdatedDate] DATETIME NOT NULL;
GO

/*

Rebuild the Stored Procedures for Lightbox

*/

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_AddLightbox]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_AddLightbox];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_AddLightbox] 
	@ModuleId INT, @GalleryName NVARCHAR(50), @GalleryDescription NVARCHAR(500), @GalleryFolder NVARCHAR(250), @DisplayOrder INT, @HideTitleDescription BIT, @LastUpdatedBy INT 
AS 
BEGIN 
	SET NOCOUNT ON;
	IF NOT @DisplayOrder > 0
	BEGIN
	    SELECT @DisplayOrder = COUNT(l.[LightboxId]) FROM {databaseOwner}[{objectQualifier}wns_lightbox] l WHERE l.[ModuleId] = @ModuleId;
	    SET @DisplayOrder = @DisplayOrder + 1;
	END

	INSERT INTO {databaseOwner}[{objectQualifier}wns_lightbox] ([ModuleId],[GalleryName],[GalleryDescription],[GalleryFolder],[DisplayOrder],[HideTitleDescription],[LastUpdatedBy],[LastUpdatedDate]) 
	VALUES (@ModuleId, @GalleryName, @GalleryDescription, @GalleryFolder, @DisplayOrder, @HideTitleDescription, @LastUpdatedBy, GETDATE());
	SELECT SCOPE_IDENTITY();
END
GO

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_UpdateLightbox]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_UpdateLightbox];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_UpdateLightbox] 
	@LightboxId INT, @ModuleId INT, @GalleryName NVARCHAR(50), @GalleryDescription NVARCHAR(500), @GalleryFolder NVARCHAR(250), @DisplayOrder INT, @HideTitleDescription BIT, @LastUpdatedBy INT 
AS 
BEGIN 
	SET NOCOUNT ON;
	IF NOT @DisplayOrder > 0
	BEGIN
	    SELECT @DisplayOrder = COUNT(l.[LightboxId]) FROM {databaseOwner}[{objectQualifier}wns_lightbox] l WHERE l.[ModuleId] = @ModuleId;
	    SET @DisplayOrder = @DisplayOrder + 1;
	END

	UPDATE {databaseOwner}[{objectQualifier}wns_lightbox] 
	SET [ModuleId] = @ModuleId,
		[GalleryName] = @GalleryName, 
		[GalleryDescription] = @GalleryDescription, 
		[GalleryFolder] = @GalleryFolder, 
		[DisplayOrder] = @DisplayOrder, 
		[HideTitleDescription] = @HideTitleDescription, 
		[LastUpdatedBy] = @LastUpdatedBy, 
		[LastUpdatedDate] = GETDATE() 
	WHERE [LightboxId] = @LightboxId;
END
GO

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_GetLightboxes]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetLightboxes];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetLightboxes] 
	@ModuleId INT 
AS 
BEGIN 
	SET NOCOUNT ON
	SELECT l.[LightboxId],l.[ModuleId],l.[GalleryName],l.[GalleryDescription],l.[GalleryFolder],l.[DisplayOrder],l.[HideTitleDescription],l.[LastUpdatedBy],l.[LastUpdatedDate] 
	FROM {databaseOwner}[{objectQualifier}wns_lightbox] l 
	WHERE l.[ModuleId] = @ModuleId 
	ORDER BY l.[DisplayOrder], l.[GalleryName];
END
GO

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_GetLightbox]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetLightbox];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetLightbox] 
	@LightboxId INT 
AS 
BEGIN 
	SET NOCOUNT ON;
	SELECT l.[LightboxId],l.[ModuleId],l.[GalleryName],l.[GalleryDescription],l.[GalleryFolder],l.[DisplayOrder],l.[HideTitleDescription],l.[LastUpdatedBy],l.[LastUpdatedDate] 
	FROM {databaseOwner}[{objectQualifier}wns_lightbox] l 
	WHERE l.[LightboxId] = @LightboxId;
END
GO

/*

Rebuild the Stored Procedures for Lightbox Images

*/

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_AddImage]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_AddImage];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_AddImage] 
	@LightboxId INT, @FileName NVARCHAR(250), @Title NVARCHAR(50), @Description NVARCHAR(500), @DisplayOrder INT, @LastUpdatedBy INT 
AS 
BEGIN 
	SET NOCOUNT ON;

	IF NOT @DisplayOrder > 0
	BEGIN
	    SELECT @DisplayOrder = COUNT(i.[ImageId]) FROM {databaseOwner}[{objectQualifier}wns_lightbox_image] i WHERE i.[LightboxId] = @LightboxId;
	    SET @DisplayOrder = @DisplayOrder + 1;
	END

	INSERT INTO {databaseOwner}[{objectQualifier}wns_lightbox_image] ([LightboxId],[FileName],[Title],[Description],[DisplayOrder],[LastUpdatedBy],[LastUpdatedDate]) 
	VALUES (@LightboxId, @FileName, @Title, @Description, @DisplayOrder, @LastUpdatedBy, GETDATE());
	SELECT SCOPE_IDENTITY();
END
GO

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_UpdateImage]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_UpdateImage];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_UpdateImage] 
	@ImageId INT, @LightboxId INT, @FileName NVARCHAR(250), @Title NVARCHAR(50), @Description NVARCHAR(500), @DisplayOrder INT, @LastUpdatedBy INT 
AS 
BEGIN 
	SET NOCOUNT ON;
	IF NOT @DisplayOrder > 0
	BEGIN
	    SELECT @DisplayOrder = COUNT(i.[ImageId]) FROM {databaseOwner}[{objectQualifier}wns_lightbox_image] i WHERE i.[LightboxId] = @LightboxId;
	    SET @DisplayOrder = @DisplayOrder + 1;
	END

	UPDATE {databaseOwner}[{objectQualifier}wns_lightbox_image] 
	SET [LightboxId] = @LightboxId,
		[FileName] = @FileName,
		[Title] = @Title, 
		[Description] = @Description, 
		[DisplayOrder] = @DisplayOrder, 
		[LastUpdatedBy] = @LastUpdatedBy, 
		[LastUpdatedDate] = GETDATE() 
	WHERE [ImageId] = @ImageId;
END
GO

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_GetImages]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetImages];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetImages] 
	@LightboxId INT 
AS 
BEGIN 
	SET NOCOUNT ON;

	SELECT i.[ImageId],i.[LightboxId],i.[FileName],i.[Title],i.[Description],i.[DisplayOrder],i.[LastUpdatedBy],i.[LastUpdatedDate] 
	FROM {databaseOwner}[{objectQualifier}wns_lightbox_image] i 
	WHERE i.[LightboxId] = @LightboxId 
	ORDER BY i.[DisplayOrder], i.[FileName];
END
GO

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_GetImageById]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetImageById];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetImageById] 
	@ImageId INT 
AS 
BEGIN 
	SET NOCOUNT ON;

	SELECT i.[ImageId],i.[LightboxId],i.[FileName],i.[Title],i.[Description],i.[DisplayOrder],i.[LastUpdatedBy],i.[LastUpdatedDate] 
	FROM {databaseOwner}[{objectQualifier}wns_lightbox_image] i 
	WHERE i.[ImageId] = @ImageId;
END
GO

IF NOT OBJECT_ID(N'{databaseOwner}[{objectQualifier}wns_lightbox_GetImageByFileName]') IS NULL
	DROP PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetImageByFileName];
GO

SET QUOTED_IDENTIFIER ON;
GO
SET ANSI_NULLS ON;
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}wns_lightbox_GetImageByFileName] 
	@LightboxId INT, @FileName NVARCHAR(250) 
AS 
BEGIN 
	SET NOCOUNT ON;

	SELECT i.[ImageId],i.[LightboxId],i.[FileName],i.[Title],i.[Description],i.[DisplayOrder],i.[LastUpdatedBy],i.[LastUpdatedDate] 
	FROM {databaseOwner}[{objectQualifier}wns_lightbox_image] i 
	WHERE i.[LightboxId] = @LightboxId AND i.[FileName] = @FileName;
END
GO

/* END OF FILE */